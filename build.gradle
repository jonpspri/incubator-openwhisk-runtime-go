/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "cz.alenkacz:gradle-scalafmt:${gradle.scalafmt.version}"
        classpath "org.antlr:antlr4:4.6"
        classpath group: 'com.google.inject', name: 'guice', version: '4.1.0'
        classpath group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
        classpath group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
        classpath group: 'commons-codec', name: 'commons-codec', version: '1.10'
        classpath group: 'commons-io', name: 'commons-io', version: '2.5'
        classpath 'com.google.guava:guava:20.0'
        classpath 'com.github.zafarkhaja:java-semver:0.9.0'
        classpath group: 'org.jsoup', name: 'jsoup', version: '1.7.2'
        classpath 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
        classpath 'com.fasterxml.jackson.core:jackson-annotations:2.8.5'
        classpath 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.5'
        classpath 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.5'
        classpath group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
        classpath group: 'net.lingala.zip4j', name: 'zip4j', version: '1.3.2'
        classpath group: 'org.apache.ant', name: 'ant', version: '1.10.0'
        classpath group: 'com.typesafe.sbt', name: 'sbt-interface', version: '0.13.13'
        classpath group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.6.3'
        classpath group: 'org.codehaus.woodstox', name: 'woodstox-core-asl', version: '4.4.1'
        classpath group: 'org.jooq', name: 'joor', version: '0.9.6'
        classpath 'com.moandjiezana.toml:toml4j:0.7.2'
        classpath files("gradle/plugins/gogradle-0.10.2.jar")
    }
}
apply plugin: 'com.github.blindpirate.gogradle'

/*plugins {
    id 'com.s390x.gogradle' version '0.10.3'
}*/

dependencies {
    golang {
        build 'github.com/sirupsen/logrus@v1.1.0'
        test 'github.com/stretchr/testify@v1.2.1'
    }
}

subprojects {
    apply plugin: 'scalafmt'
    scalafmt.configFilePath = gradle.scalafmt.config
}

golang {
  packagePath = 'github.com/apache/incubator-openwhisk-runtime-go'
  goVersion = '1.11.6'
}

/*
    The OpenWhiskPlatform class is a utility class to make the rest of what
    happens with platforms a bit more understandable.  A "Platform" is a tuple
    of an operating system and a processor.  Currently, the OpenWhisk CLI
    supports three OS's:  Linux, Mac/Darwin, and Windows.  It supports x86
    (32-bit or 64-bit) on all OS's.  On Linux, it also support System Z (s390x),
    PowerPC (ppc64le), and ARM (32-bit and 64-bit) architectures.
    Different contexts use different codings to refer to these architectures --
    the class attempts to provide and interpret all needed codings.  Internal
    storage is in "GO" format:
        OS: linux, darwin, windows
        Arch: 386, amd64, s390x, ppc64le, arm64
 */
class OpenWhiskPlatform {
    String goOs
    String goArch

    /*
        Create a platform for the local platform
     */
    OpenWhiskPlatform() {
        this(System.properties['os.name'], System.properties['os.arch'])
    }

    OpenWhiskPlatform(String platformSpec) {
        this(*platformSpec.split('-'))
    }

    OpenWhiskPlatform(String inOs, String inArch) {
        goOs=inOs.toLowerCase()
                 .replaceAll(~/^mac.*$/,'darwin')
                 .replaceAll(~/^.*n[ui]x.*$/,'linux')
        goArch=inArch.toLowerCase()
                     .replaceAll('x86_64','amd64')
                     .replaceAll('i386','386')
                     .replaceAll('x86_32','386')
                     .replaceAll('aarch64','arm64')
    }

    /**
     * Return the Openwhisk OS for this Platform
     */
    String getOwOs() {
        ((goOs == 'darwin') ? 'mac' : goOs)
    }

    String getGoPlatform() {
        "${goOs}-${goArch}"
    }
}

goBuild.dependsOn goVendor

goBuild {
   targetPlatform = ["linux-${(new OpenWhiskPlatform()).goArch}".toString()]
   go """build -o common/proxy -ldflags '-extldflags "-static"' main/proxy.go"""
}
